/*
 * Copyright 2020-present, Nike, Inc.
 * All rights reserved.
 * 
 * This source code is licensed under the Apache-2.0 license found in
 * the LICENSE file in the root directory of this source tree.
 */

plugins {
    id 'java'
    id 'application'
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id 'idea'
}

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

//http://www.gradle.org/docs/current/userguide/java_plugin.html
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

mainClassName = 'com.nike.mycli.Main'

startShadowScripts {
    applicationName = 'mycli'
}

dependencies {
    implementation 'com.nike:gimme-a-cli:1.0.0'
    implementation "ch.qos.logback:logback-classic:1.2.3"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

shadowJar {
    def releaseVersion = version
    doFirst {
        ant.replace(file: "$buildDir/resources/main/mycli.properties", token: "@@RELEASE@@", value: releaseVersion)
    }
}


/* Tasks for running CLI commands from Gradle */

class CliExec extends JavaExec {
    CliExec() {
        classpath = project.sourceSets.main.runtimeClasspath
        main = project.mainClassName
        group = "CLI"
    }
}

// Example of defining a CLI command as a Gradle task, e.g., ./gradlew helloWorldFromGradle
task helloWorldFromGradle(type: CliExec) {
    description = "Example of defining a CLI command as a Gradle task"

    // arguments to pass to the application
    args 'hello', '--name', 'world from gradle'
}
